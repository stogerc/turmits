;--------------------------------------
; Thurmit name: Line of Life 
; Ђўв®а: „¬ЁваЁ© Џ ЇЁзҐў (Dmitri.Papichev@iname.com)
;--------------------------------------
; Thurmit plays Life (J.Conway's rules)
;
;-------------------------
; Pattern: Horizontal Line
;-------------------------
;
;----- begin Life
;
;----- draw Life pattern
A  0 15  0 Ѓ 
Ѓ  0 15  0 ‚
‚  0 15  0 ѓ
ѓ  0 15  0 „
„  0 15  0 …
…  0 15  0 р
р  0 15  0 §
§  0 15  0 ©
©  0 15  0 < 
<  0 15  0 †
†  0 15  0 ‡
‡  0 15  0 €
€  0 15  0 Љ
Љ  0 15  0 ‹
‹  0 15  0 Њ
Њ  0 15  0 Ќ
Ќ  0 15  0 Ћ
Ћ  0 15  0 Џ
Џ  0 15  0 ђ
ђ  0 15  0 ‘
‘  0 15  0 ’
’  0 15  0 “
“  0 15  0 ”
”  0 15  0 •
•  0 15  0 –
–  0 15  0 :
:  0 15  0 "
"  0 15  0 |
|  0 15  0 \
\  0 15  0 {
{  0 15  0 }
}  0 15  0 Ч
Ч  0 15  0 Ш
Ш  0 15  0 Щ
Щ  0 15  0 Ъ
Ъ  0 15  0 Ы
Ы  0 15  0 Ь
Ь  0 15  0 Э
Э  0 15  0 Я
Я  0 15  0 Ж
Ж  0 15  0 [
;----- end of draw Life pattern 
;
;- call ApproachFrame
;- call DrawFrame
;
;--- back pass (to draw new generation)
x  0  0  0 x
x  8 15  0 x
x  7  0  0 x 
x 15 15  0 x
x  2  2 -1 u
u  2  2 -1 t
t  2  2 -1 о ;- came to upper left corner
t  0  0  0 s
t  8 15  0 s
t  7  0  0 s
t 15 15  0 s
s  0  0  0 s
s  8 15  0 s
s  7  0  0 s
s 15 15  0 s
s  2  2  1 r
r  2  2  1 x
;
;----- forward pass (to calculate new generation)
;
;--- start exploring neighbors
;
;- moving from left to right
о  0  0  1 Ў  
о 15 15  1 Ў 
;
;- temporary coloring of the basic cell indicates right to left direction
;- color  6 = empty cell
;- color 11 = live cell
;  
;- moving from right to left
®  0  6  1 Ў  
® 15 11  1 Ў 
;--- 
;
;--- faced right frame
о  2  2  1 Ё
Ё  2  2  1 ® 
;--- faced left frame
®  2  2 -1 Є
Є  2  2 -1 о
;--- came to lower right corner
Є  0  0 -1 «
«  0  0 -1 ¬
¬  2  2 -1 ­
­  2  2  1 x
;
;--- color marks
;- color  0 = empty cell
;- color  7 = dying cell (will die next generation)
;- color 15 = live cell
;- color  8 = aborning cell (will be born next generation)
;
;--- temporary coloring first/last neighbor cell 
;    (to the right of the basic cell) - 
;    in order to know when to come back to the basic cell
Ў  0  1  1 в ;- empty 
Ў  8  5  1 в ;- aborning
Ў  2  3  1 в ;- frame 
Ў  7  4  1 д ;- dying
Ў 15 10  1 д ;- live
;
;----- final processing of the basic cell
;
;--- no neighbors or 1 neighbor found
;- empty -> empty
й  0  0  1 о  
й  6  0  1 ® 
;- live -> dying
й 15  7  1 о   
й 11  7  1 ® 
;
;--- 2 neighbors found
;- empty -> empty
л  0  0  1 о   
л  6  0  1 ®
;- live -> live
л 15 15  1 о 
л 11 15  1 ® 
;
;--- 3 neighbors found
;- empty -> aborning
м  0  8  1 о 
м  6  8  1 ®
;- live -> live
м 15 15  1 о 
м 11 15  1 ®
;
;--- 4 neighbors or more found
;- empty -> empty
н  0  0  1 о  
н  6  0  1 ®
;- live -> dying
н 15  7  1 о 
н 11  7  1 ®
;
;----- end of final processing of the basic cell
;
;----- processing _corner_ neighbor cells
;
;--- no neighbors found before
;- neighbor detected
в 15 15  1 Ј
в  7  7  1 Ј
;- neighbor not detected
в  0  0  1 ў
в  8  8  1 ў
в  2  2  1 ў
;
;--- 1 neighbor found before
;- neighbor detected
д 15 15  1 ¤ 
д  7  7  1 ¤ 
;- neighbor not detected
д  0  0  1 Ј
д  8  8  1 Ј
д  2  2  1 Ј
;
;--- 2 neighbors found before
;- neighbor detected
ж 15 15  1 с 
ж  7  7  1 с 
;- neighbor not detected
ж  0  0  1 ¤
ж  8  8  1 ¤
ж  2  2  1 ¤
;
;--- 3 neighbors found before
;- neighbor detected
з 15 15  1 ¦ 
з  7  7  1 ¦ 
;- neighbor not detected
з  0  0  1 с
з  8  8  1 с
з  2  2  1 с
;
;--- 4 neighbors or more found before
;- neighbor detected
и 15 15  1 ¦ 
и  7  7  1 ¦ 
;- neighbor not detected
и  0  0  1 ¦
и  8  8  1 ¦
и  2  2  1 ¦
;----- end of processing _corner_ neighbor cells
;
;----- processing _side_ neighbor cells
;
;--- no neighbors found before
;- neighbor detected
ў 15 15  0 д
ў  7  7  0 д
;- neighbor not detected
ў  0  0  0 в
ў  8  8  0 в
ў  2  2  0 в
;- come back to the basic cell
ў  1  0  1 й 
ў  3  2  1 й  
ў  5  8  1 й  
ў 10 15  1 й 
ў  4  7  1 й  
;
;--- 1 neighbor found before
;- neighbor detected
Ј 15 15  0 ж 
Ј  7  7  0 ж   
;- neighbor not detected
Ј  0  0  0 д
Ј  8  8  0 д
Ј  2  2  0 д
;- come back to the basic cell
Ј  1  0  1 й 
Ј  3  2  1 й  
Ј  5  8  1 й  
Ј 10 15  1 й 
Ј  4  7  1 й  
;
;--- 2 neighbors found before
;- neighbor detected
¤ 15 15  0 з 
¤  7  7  0 з   
;- neighbor not detected
¤  0  0  0 ж
¤  8  8  0 ж
¤  2  2  0 ж
;- come back to the basic cell
¤  1  0  1 л 
¤  3  2  1 л 
¤  5  8  1 л 
¤ 10 15  1 л 
¤  4  7  1 л
;  
;--- 3 neighbors found before
;- neighbor detected
с 15 15  0 и 
с  7  7  0 и   
;- neighbor not detected
с  0  0  0 з
с  8  8  0 з
с  2  2  0 з
;- come back to the basic cell
с  1  0  1 м 
с  3  2  1 м 
с  5  8  1 м 
с 10 15  1 м 
с  4  7  1 м  
;
;--- 4 neighbors or more found before
;- neighbor detected
¦ 15 15  0 и 
¦  7  7  0 и   
;- neighbor not detected
¦  0  0  0 и
¦  8  8  0 и
¦  2  2  0 и
;- come back to the basic cell
¦  1  0  1 н 
¦  3  2  1 н 
¦  5  8  1 н 
¦ 10 15  1 н 
¦  4  7  1 н  
;----- end of processing _side_ neighbor cells
;
;----- ApproachFrame
[  0  0  0 ?
?  0  0  1 Q
;----- end of ApproachFrame
;
;----- DrawFrame
Ђ  0  2  1 1
1  0  2  0 2
2  0  2  0 3
3  0  2  0 4
4  0  2  0 5
5  0  2  0 6
6  0  2  0 7
7  0  2  0 8
8  0  2  0 9
9  0  2  0 0
0  0  2  0 !
!  0  2  0 @
@  0  2  0 -
-  0  2  0 $
$  0  2  0 %
%  0  2  0 ^
^  0  2  0 &
&  0  2  0 *
*  0  2  0 (
(  0  2  0 )
)  0  2  0 +
+  0  2  0 =
=  0  2  0 Q
Q  0  2  0 W
W  0  2  0 E
E  0  2  0 R
R  0  2  0 T
T  0  2  0 Y
Y  0  2  0 U
U  0  2  0 I
I  0  2  0 O
O  0  2  0 P
P  0  2  0 S
S  0  2  0 D
D  0  2  0 F
F  0  2  0 G
G  0  2  0 H
H  0  2  0 J
J  0  2  0 K
K  0  2  0 L
L  0  2  0 Z
Z  0  2  0 X
X  0  2  0 C
C  0  2  0 Ђ
;
Ђ  2  2  1 a
a  2  2  1 x ;go to back pass  
;
;--- redraw partially frame side
(  2  2  0 )
)  2  2  0 +
+  2  2  0 =
=  2  2  0 Q
Q  2  2  0 W
W  2  2  0 E
E  2  2  0 R
R  2  2  0 T
T  2  2  0 Y
Y  2  2  0 U
U  2  2  0 I
I  2  2  0 O
O  2  2  0 P
P  2  2  0 S
S  2  2  0 D
D  2  2  0 F
F  2  2  0 G
G  2  2  0 H
H  2  2  0 J
J  2  2  0 K
K  2  2  0 L
L  2  2  0 Z
Z  2  2  0 X
X  2  2  0 C
C  2  2  0 Ђ
;--- end of DrawFrame
;
;----- end Life


